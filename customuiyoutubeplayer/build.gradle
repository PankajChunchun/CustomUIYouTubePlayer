apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

//Project Properties
group 'com.dptsolutions.customuiyoutubeplayer'
version '0.1'
def projectName = 'custom-ui-youtube-player'
def projectFriendlyName = 'Custom UI YouTube Player'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName project.version
        //This is for vector compat drawables w/ 1.5 Gradle plugin
        generatedDensities = []
    }
    buildTypes {

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // This is handled for you by the 2.0+ Gradle Plugin
    aaptOptions {
        additionalParameters "--no-version-vectors"
    }
}

bintray {
    user = project.hasProperty('bintrayUserName') ? project.property('bintrayUserName') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = projectName
        desc = 'An Android library containing a Fragment extending YouTubePlayerFragment from Google\'s YouTube Android Player API, allowing you to easily play YouTube videos in your application with a customizable player controls overlay. For API 16+.'
        licenses = ['Apache-2.0']
        labels = ['android', 'aar']
        websiteUrl = project.property('siteUrl')
        issueTrackerUrl = project.property('issueTrackerUrl')
        vcsUrl = project.property('gitUrl')
        version {
            name = project.version
            desc = projectFriendlyName
            released  = new Date()
        }
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name = projectFriendlyName
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id mavenDeveloperId
                        name mavenDeveloperName
                        email mavenEmail
                    }
                }

                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

task listPublishingProperties {
    println 'bintrayUserName - ' + bintrayUserName
    println 'bintrayApiKey - ' + bintrayApiKey
    println 'mavenDeveloperId - ' + mavenDeveloperId
    println 'mavenDeveloperName - ' + mavenDeveloperName
    println 'mavenEmail - ' + mavenEmail
    println 'siteUrl - ' + siteUrl
    println 'gitUrl - ' + gitUrl
    println 'issueTrackerUrl - ' + issueTrackerUrl
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.2.1'
    compile files('libs/YouTubeAndroidPlayerApi.jar')
}
